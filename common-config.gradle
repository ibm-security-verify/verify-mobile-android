android {
    namespace = "com.ibm.security.verifysdk." + project.path.replaceFirst("^:sdk:", "").replace(":", ".")
    testNamespace = "com.ibm.security.verifysdk." + project.path.replaceFirst("^:sdk:", "").replace(":", ".") + ".test"

    compileSdk = 35
    buildFeatures.buildConfig = true

    defaultConfig {
        minSdk = 26
        buildConfigField "String", "VERSION_NAME", "\"${rootProject.ext.versionName}\""
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        named("release") {
            minifyEnabled = false
            proguardFiles(
                    getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro"
            )
        }
        named("debug") {
            enableUnitTestCoverage = true
            enableAndroidTestCoverage = true
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

configurations.configureEach {
    resolutionStrategy {
        force("org.slf4j:slf4j-api:1.7.36")
        force("org.slf4j:slf4j-android:1.7.36")
    }
}

dependencies {
    testImplementation(libs.junit)
    androidTestImplementation(libs.skyscreamer.jsonassert)
    androidTestImplementation(libs.androidx.junit)
    androidTestImplementation(libs.slf4j.android)
    androidTestImplementation(libs.slf4j.api)
}
